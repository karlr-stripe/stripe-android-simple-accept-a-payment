plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

// Read values from gradle.properties or system environment variable
// see e.g. https://github.com/stripe/stripe-android/tree/master/example#configure-the-app
def getBackendUrl() {
    return readProperty('STRIPE_EXAMPLE_PAYMENTS_BACKEND_URL')
}

def getPublishableKey() {
    return readProperty('STRIPE_EXAMPLE_PAYMENTS_PUBLISHABLE_KEY')
}

private def readProperty(name) {
    final String propValue
    if (hasProperty(name)) {
        propValue = property(name)
    } else {
        propValue = System.getenv(name)
    }

    return propValue?.trim() ? propValue : ""
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.example.stripepaymentintegration"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        // https://medium.com/@takeoffandroid/securing-api-key-in-android-without-pushing-to-remote-repository-git-4237db104d91
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "BACKEND_URL", "\""+getBackendUrl()+"\""
            variant.buildConfigField "String", "PUBLISHABLE_KEY", "\""+getPublishableKey()+"\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Stripe Android SDK
    implementation 'com.stripe:stripe-android:16.1.1'
    // https://developer.android.com/training/volley
    implementation 'com.android.volley:volley:1.1.1'
    // used to format PaymentIntents from Stripe API response
    implementation 'com.google.code.gson:gson:2.8.6'
}